*** Settings ***
Documentation     This resource file contains keywords and generic Selenium Webdriver settings used to execute
...               data-driven tests. Each of the keywords describe what they do in a single line [Documentation]
...               keyword.
...               NOTE that the browser used is PhantomJS. Chrome and Firefox require Xvfb and that is way too much
...               work for now! PhantomJS however appears to have issues with taking screenshots.
Library           Selenium2Library

*** Variables ***
${BROWSER}                  PhantomJS
${WEBDRIVER_DELAY}          0
${IMPLICIT_WAIT}            1
${LANDING_PAGE_URL}         http://www.ird.govt.nz/calculators/tool-name/tools-t/calculator-tax-rate.html?id=homepage
${LANDING_PAGE_TITLE}       Tax on annual income calculator (by tool name)
${CALCULATOR_PAGE_TITLE}    Enter your tax details - Inland Revenue - Te Tari Taake.

*** Keywords ***
Open Browser To Landing Page
    [Documentation]               This keyword navigates to the landing page and begins a new calculation using the specified browser.
    Open Browser                  ${LANDING_PAGE_URL}    ${BROWSER}
    Maximize Browser Window
    Set Selenium Speed            ${WEBDRIVER_DELAY}
    Set Selenium Implicit Wait    ${IMPLICIT_WAIT}
    Title Should Be               ${LANDING_PAGE_TITLE}
    Start A New Calculation

Start A New Calculation
    [Documentation]    This begings a brand new session for calculating TOAI, and opens in a separate window.
    Click Link         link=Start >
    Select Window      title=${CALCULATOR_PAGE_TITLE}
    Maximize Browser Window

Enter Taxpayer Information
    [Documentation]              This lets you enter tax payer information (all of which are supplied as arguments).
    [Arguments]                  ${category}    ${year}    ${income}    ${name}=${EMPTY}
    Select From List By Value    id=qss2Interviews_ScreensFile2_xintglobalglobal2    ${category}
    Select From List By Value    id=qss2Interviews_ScreensFile2_xintglobalglobal3    ${year}
    Input Text                   id=qss2Interviews_ScreensFile2_xintglobalglobal4    ${income}
    Input Text                   id=qss2Interviews_ScreensFile2_xintglobalglobal10   ${name}

Validate Income Tax
    [Documentation]           Checks to make sure that the income tax computed matches expected data.
    [Arguments]               ${tax}
    Element Text Should Be    xpath=//table[@class='income-tax-table']/tbody/tr[8]/td[3]/b    ${tax}

Perform Another Calculation
    [Documentation]    This keyword ends the current session by asking the calculator to perform another calculation.
    Click Link         link=Perform another calculation
    Click Button       id=ACTIONOK
    Click Link         link=Click here to use the calculator

Submit Information
    [Documentation]    Self explanatory?
    Click Button       id=submit