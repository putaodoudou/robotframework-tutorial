*** Settings ***
Documentation     A test suite with edge and/or data-driven cases for verifying the
...               Tax on Annual Income Calculator.
...
...               This test suite has a workflow that is created using keywords from
...               the imported resource file. This also serves to illustrate multi-line
...               or block documentation.
Suite Setup       Open Browser To Landing Page
Suite Teardown    Close Browser
Test Teardown     Perform Another Calculation
Test Template     Tax on Annual Income
Resource          resource_data-driven.txt


*** Test Cases ***                            Category      Year                             Income      Tax                Name
Individual 2014-2015 Floor                    individual    1 April 2014 to 31 March 2015    1           $0.10
    [Documentation]                           Notice how you can add documentation inside of test cases using the same tags?
    [Tags]                                    functional    floor

Individual 2014-2015 Ceiling                  individual    1 April 2014 to 31 March 2015    99999999    $32,990,919.67
    [Tags]                                    functional    ceiling

Individual 2014-2015 Tax Slab ONE Failure     individual    1 April 2014 to 31 March 2015    14000       ${EMPTY}
    [Documentation]                           This test case is intended to fail.; Also notice how we're passing a NULL value for the assertion here?
    [Tags]                                    failure

Individual 2014-2015 Tax Slab ONE Edge ONE    individual    1 April 2014 to 31 March 2015    13999       $1,469.89
    [Tags]                                    edge

Individual 2014-2015 Tax Slab ONE             individual    1 April 2014 to 31 March 2015    14000       $1,470.00
    [Tags]                                    functional

Individual 2014-2015 Tax Slab ONE Edge TWO    individual    1 April 2014 to 31 March 2015    14001       $1,470.17
    [Tags]                                    edge

Individual 2014-2015 Tax Slab TWO Failure     individual    1 April 2014 to 31 March 2015    48001       $7,420.00
    [Documentation]                           This test case is DEFINITELY going to fail!
    [Tags]                                    failure

Individual 2014-2015 Optional Arguments       individual    1 April 2014 to 31 March 2015    90000       $20,620.00         Alice
    [Documentation]                           This test case also includes the optional argument 'Name', the default value of which is NULL or ${EMPTY}
    [Tags]                                    optional    functional


*** Keywords ***
Tax On Annual Income
    [Documentation]               This is the test case template that is used to drive each of the data driven tests in this suite.
    [Arguments]                   ${category}    ${year}    ${income}    ${tax}    ${name}=${EMPTY}
    Enter Taxpayer Information    ${category}    ${year}    ${income}    ${name}
    Submit Information
    Validate Income Tax           ${tax}